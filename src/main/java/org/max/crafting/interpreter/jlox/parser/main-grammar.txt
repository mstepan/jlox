// Context-Free Grammar for jlox language
//
// we will try to follow "C" precedence levels (see https://en.cppreference.com/w/c/language/operator_precedence)
//
//

program -> (declaration)* EOF

//====== DECLARATIONS & STATEMENTS ============

declaration -> funcDeclaration | varDeclaration | statement

funcDeclaration -> "fn" function

function -> IDENTIFIER fnParamsAndBody

fnParamsAndBody -> "(" parameters? ")" block

parameters -> IDENTIFIER (", " IDENTIFIER)*

varDeclaration -> "var" IDENTIFIER ("=" expression)? ";"

statement -> exprStmt | block | ifStatement | whileStatement | forStatement | returnStatement

exprStmt -> expression ";"

block -> "{" declaration* "}"

ifStatement -> "if" "(" expression ")" statement ("else" statement)?

whileStatement -> "while" "(" expression ")" statement

forStatement -> "for" "(" (varDeclaration | exprStmt | ";") expression? ";" expression?  ")" statement

returnStatement -> "return" expression? ";"

//====== EXPRESSIONS (all expression are left recursive by default) ============

expression -> comma

comma -> assignment ("," comma)*

// assignment is right recursive
assignment -> IDENTIFIER "=" assignment | logic_or

logic_or -> logic_and ("or" logic_and)*

logic_and -> equality ("and" equality)*

equality -> comparison  (("==" | "!=") comparison)*

comparison -> term ((">" | "<=" | "<" | "<=") term)*

term -> factor (("+" | "-") factor)*

factor -> unary (("*" | "/" ) unary)*

unary -> ("!" | "-") unary | call | functionExpr

functionExpr -> "fn" fnParamsAndBody

// function call has highest precedence
call -> primary ( "(" arguments? ")" )*

arguments -> expression (", " expression)*

primary -> STRING | NUMBER | "true" | "false" | nil | "(" expression ")" | IDENTIFIER
